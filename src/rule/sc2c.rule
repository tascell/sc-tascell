;;; Copyright (c) 2008-2011 Tasuku Hiraishi <tasuku@media.kyoto-u.ac.jp>
;;; All rights reserved.

;;; Redistribution and use in source and binary forms, with or without
;;; modification, are permitted provided that the following conditions
;;; are met:
;;; 1. Redistributions of source code must retain the above copyright
;;;    notice, this list of conditions and the following disclaimer.
;;; 2. Redistributions in binary form must reproduce the above copyright
;;;    notice, this list of conditions and the following disclaimer in the
;;;    documentation and/or other materials provided with the distribution.

;;; THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND
;;; ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;;; ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR BE LIABLE
;;; FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
;;; DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
;;; OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
;;; HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
;;; LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
;;; OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
;;; SUCH DAMAGE.

;;;; SC Compiler: SC-0/1 to C translator
(scr:require "SC2C-DEFS")

(define-ruleset sc2c ()
  (entry 'sc-program)
  )

(defvar *sc2c-typename-list* '())   ;; list of scid

(defrule sc-program sc2c
  (#?(,@list)
   (string+-rec
    (let ((*sc2c-typename-list* '()))
      (translation-unit list)))))

(defrule translation-unit sc2c
  ((#?(,@ext-decl-list))
   (mapcar #'(lambda (x)
               (aif (external-declaration? x)
                   it
                 (list #\Newline "/* Error!" #\Newline
                       (write-to-string x) 
                       #\Newline "*/" #\Newline)))
           ext-decl-list)))

(defrule external-declaration sc2c
  ((#?,decl[declaration])
   (get-retval decl))
  ((#?,cexp[c-expression])
   (list (get-retval cexp) #\Newline)))

;;;; 2.Declarations
;; declaration
(defrule declaration sc2c
  ((#?,i-decl[inlined-declaration])
   (get-retval i-decl))
  ;; 'def' abbreviation (SC-1)
  ((#?(,id[identifier] ,texp[type-expression] { ,init[initializer] })
    #?(,f-id[function-identifier]
            (,fn-tag[function-tag] ,texp[type-expression] ,@texp-list[type-expression]
                    {,va-arg[vaarg-tag]})
            ,@bi-list[block-item])
    #?(,su-spec[struct-or-union-specifier]
               ,@sdecl-list[struct-declaration])
    #?(,en-spec[enum-specifier] ,@en-list[enumerator]))
   (inlined-declaration ~(def ,@x)))
  )

;; inlined-declaration
(defrule inlined-declaration sc2c
  ;; variable definitions/declarations
  ((#?(,scs[storage-class-specifier]
           ,id[identifier] ,texp[type-expression] { ,init[initializer] } ))
   (let ((scs-str (get-retval scs))
         (idtexp-str (funcall (get-retval texp) (get-retval id)))
         (init-str (if-pattern-variable init
                       (list " = "
                             (sc2c:parenthesize
                              (multiple-value-list (get-retval init))
                              15))
                     "")))
     (list scs-str " " idtexp-str init-str ";" #\Newline)))
  ;; function definitions/declarations
  ((#?(,scs[storage-class-specifier]
           ,func-id[function-identifier]
           (,fn-tag[function-tag] ,texp[type-expression] ,@texp-list[type-expression]
                   { ,va-arg[vaarg-tag] } )
           { :attr ,f-attr[function-attribute] }
           { ,r-decl[register-declarator] }
           ,@bi-list[block-item]))
   (let ((scs-retval (multiple-value-list (get-retval scs)))
         (fid-str (car (get-retval func-id)))
         (texp-fun (get-retval texp))
         (arg-list (combine-each-nth    ; list of (<texp> "<id>")
                    (list 
                     (get-retval texp-list)
                     (cdr (get-retval func-id)))
                    :n (max (length texp-list) 
                            (length (cdr (get-retval func-id))))))
         (inline-str  (if-pattern-variable f-attr
                          (list (get-retval f-attr) " ")
                        ""))
         (register-list (if-pattern-variable r-decl
                            (get-retval r-decl)
                          nil))
         (vaarg-str (if-pattern-variable va-arg ", ..." "")))
     (let ((scs-str (first scs-retval))
           (arg-str 
            (list
             (separate-list 
              (mapcar 
               #'(lambda (xx)
                   (list
                    (if (member (second xx) register-list
                                :test #'string=)
                        "register "
                      "")
                    (if (first xx)
                        (funcall (first xx)
                                 (if (second xx) (second xx) ""))
                      (second xx))))
               arg-list)
              ", ")
             vaarg-str))      
           (body-str (if (and (eq 'decl (second scs-retval))
                              (null bi-list))
                         ";"
                       (list #\Newline "{" #\Newline 
                             (get-retval bi-list)
                             "}"))))
       (list #\Newline
             inline-str scs-str " "
             (funcall texp-fun
                      (list fid-str " (" arg-str ")"))
             body-str
             #\Newline
             )))
   )
  ;; struct/union definiitons/declarations
  ((#?(,dd[def-or-decl] ,su-spec[struct-or-union-specifier]
          ,@sdcl-list[struct-declaration]))
   (if (and (eq 'decl (nth-multiple-value 1 (get-retval dd)))
            (null sdcl-list))
       (list (get-retval su-spec) ";" #\Newline)
     (list #\Newline (get-retval su-spec)
           "{" #\Newline (get-retval sdcl-list) "};"
           #\Newline)))
  ;; enum definitions
  ((#?(def ,espec[enum-specifier] ,@en-list[enumerator]))
   (list (get-retval espec) 
         " { "
         (separate-list (get-retval en-list) ", ")
         "};" #\Newline))
  ;; compound variable definitions/declarations (SC1 ext)
  ((#?(,spec[compound-storage-class-specifier]
            ,texp ,@idec-list[init-declarator]))
   (let ((spec-retval (get-retval spec))
         (idec-retval-list (get-retval idec-list)))
     (mapcar #'(lambda (xx)
                 (inlined-declaration
                  `(,spec-retval ,(first xx) ,texp ,@(cdr xx)))) 
             idec-retval-list)) )
  ;; deftype
  ((#?(,dt-tag[deftype-tag] ,id[identifier] ,texp[type-expression]))
   (push id *sc2c-typename-list*)
   (list (get-retval dt-tag) " "
         (funcall (get-retval texp)
                  (get-retval id)) ";" #\Newline) )
  ;; deftype (struct)
  ((#?(,dt-tag[deftype-tag] ,id[identifier] ,su[struct-or-union]
              ,@sdcl-list[struct-declaration]))
   (push id *sc2c-typename-list*)
   (list (get-retval dt-tag) " "
         (get-retval su) " {" #\Newline
         (get-retval sdcl-list) " } "
         (get-retval id) ";" #\Newline) )
  ;; deftype (enum)
  ((#?(,dt-tag[deftype-tag] ,id[identifier] enum ,@en-list[enumerator]))
   (push id *sc2c-typename-list*)
   (list (get-retval dt-tag) " enum { "
         (separate-list (get-retval en-list) ", ") " } "
         (get-retval id) ";" #\Newline) )
  )

(defrule function-attribute sc2c
  ((#?inline)   "inline"))

(defrule register-declarator sc2c
  ((#?(,reg[register-tag] ,@id-list[identifier]))
   (get-retval id-list)))

(defrule register-tag sc2c
  ((#?register) "register"))

;; funcion-identifier
(defrule function-identifier sc2c
  ((#?,id[identifier])
   (list (get-retval id)))
  ((#?(,@id-list[identifier]))
   (get-retval id-list))
  )

;; init-declarator
(defrule init-declarator sc2c
  ((#?,id[identifier])
   (list id))
  ((#?(,id[identifier] ,init))
   (list id init))
  )

(defrule declaration-tag sc2c
  ((#?,tag[storage-class-specifier]
    #?,tag[deftype-tag]
    #?,tag[compound-storage-class-specifier]) ;sc1
   (get-retval tag)))

(defrule deftype-tag sc2c
  ((#?deftype)       "typedef"))

;; def-or-decl
(defrule def-or-decl sc2c
  ((#?def)   (values "" 'def))
  ((#?decl)  (values "" 'decl)))

;; storage-class-specifier
(defrule storage-class-specifier sc2c
  ((#?,dd[def-or-decl])        (get-retval dd))
  ((#?extern #?extern-def)     (values "extern" 'def))
  ((#?extern-decl)             (values "extern" 'decl))
  ((#?static #?static-def)     (values "static" 'def))
  ((#?static-decl)             (values "static" 'decl))
  ((#?auto #?auto-def)         (values "auto" 'def))
  ((#?register #?register-def) (values "register" 'def))
  )

;; compound-storage-class-specifier
;; defs to def
(defrule compound-storage-class-specifier sc2c
  ((#?defs)          ~def)
  ((#?extern-defs)   ~extern)
  ((#?static-defs)   ~static)
  ((#?auto-defs)     ~auto)
  ((#?register-defs) ~register))

;; struct-declaration
(defrule struct-declaration sc2c
  ((#?,decl[declaration]) (get-retval decl))
  ((#?:bit)               " : ")
  ((#?,exp[expression])
   (let ((exp-retval (multiple-value-list (get-retval exp)))
         (assoc 14))
     (list (sc2c:parenthesize exp-retval assoc) #\Newline)))
  )

;; enumerator
(defrule enumerator sc2c
  ((#?,en-const[enumeration-constant])
   (get-retval en-const))
  ((#?(,en-const[enumeration-constant] ,exp[expression]))
   (let ((exp-retval (multiple-value-list (get-retval exp))))
     (list (get-retval en-const)
           " = " (sc2c:parenthesize exp-retval 15))))
  )

;; enumeration-constant
(defrule enumeration-constant sc2c
  ((#?,id[identifier])
   (get-retval id)))

;; designator
(defrule designator sc2c
  ((#?(aref-this ,@exp-list[expression]))
   (separate-list (get-retval exp-list) "][" "[" "]"))
  ((#?(fref-this ,@id-list[identifier]))
   (separate-list (get-retval id-list) "." "." ""))
  ((#?(aref ,desig[designator] ,@exp-list[expression]))
   (list (get-retval desig)
         (separate-list (get-retval exp-list) "][" "[" "]")))
  ((#?(fref ,desig[designator] ,@id-list[identifier]))
   (list (get-retval desig)
         (separate-list (get-retval id-list) "." "." "")))
  )

(defrule designator-tag sc2c
  ((#?aref-this #?fref-this #?aref #?fref)
   x))

;; designated-initializer
(defrule designated-initializer sc2c
  ((#?(,desig[designator] ,init[initializer]))
   (values
    (list (get-retval desig) " = " (get-retval init))
    (nth-multiple-value 1 (assignment-operator! ~=))))
  ((#?,init[initializer])
   (get-retval init))
  )

;;compound-initializer
(defrule compound-initializer sc2c
  ((#?(array ,@init-list[designated-initializer])
    #?(struct ,@init-list[designated-initializer]))
   (let ((init-retval-list
          (combine-each-nth
           (multiple-value-list (get-retval init-list)))))
     (values
      (list
       "{ " 
       (separate-list (mapcar #'(lambda (xx)
                                  (sc2c:parenthesize xx 16))
                              init-retval-list)
                      ", ")
       " }")
      0)))
  )

;; initializer
(defrule initializer sc2c
  ((#?,cmp-init[compound-initializer])
   (get-retval cmp-init))
  ((#?,exp[expression])
   (get-retval exp))
  )

;;;; 3.Type-expressions
(defrule type-expression sc2c
  ((#?,tspec[type-specifier])
   (values
    #'(lambda (cid) (list (get-retval tspec) " " cid)) 
    17))
  ((#?(,ar-tag[array-tag] ,texp[type-expression]))
   (let ((retval-texp (multiple-value-list (get-retval texp)))
         (assoc 1))
     (values
      #'(lambda (cid)
          (funcall (first retval-texp)
                   (sc2c:parenthesize-t (list cid "[]")
                                        (second retval-texp)
                                        assoc)))
      assoc)))
  ((#?(,ar-tag[array-tag] ,texp[type-expression] ,@exp-list[expression]))
   (let ((retval-texp (multiple-value-list (get-retval texp)))
         (assoc 1))
     (values
      #'(lambda (cid)
          (funcall
           (first retval-texp)
           (sc2c:parenthesize-t (list cid
                                      "["
                                      (separate-list (get-retval exp-list) "][")
                                      "]")
                                (second retval-texp)
                                assoc)))
      assoc)))
  ((#?(,ptr-tag[pointer-tag] ,texp[type-expression]))
   (let ((retval-texp (multiple-value-list (get-retval texp)))
         (assoc 2))
     (values
      #'(lambda (cid)
          (funcall (first retval-texp)
                   (sc2c:parenthesize-t (list (get-retval ptr-tag) cid)
                                        (second retval-texp)
                                        assoc)))
      assoc)))
  ((#?(,fn-tag[function-tag] ,texp1[type-expression] ,@texp-list[type-expression]
              {,va-arg[vaarg-tag]}))
   (let ((retval-texp1 (multiple-value-list (get-retval texp1)))
         (assoc 1))
     (values
      #'(lambda (cid)
          (funcall
           (first retval-texp1)
           (sc2c:parenthesize-t (list cid
                                      "( "
                                      ;; 引数リスト
                                      (separate-list (mapcar #'(lambda (xx)
                                                                 (funcall xx ""))
                                                             (get-retval texp-list))
                                                     ", ")
                                      ;; "..."
                                      (if-pattern-variable va-arg
                                          (list ", " (get-retval va-arg))
                                        "")
                                      " )")
                                (second retval-texp1)
                                assoc)))
      assoc)))
  ((#?(,@tq-list[type-qualifier] ,texp[type-expression]))
   (values
    #'(lambda (cid)
        (funcall (get-retval texp)
                 (list " " (separate-list (get-retval tq-list) " ")
                       " " cid)))
    17))
  )

(defrule array-tag sc2c
  ((#?array) x))

(defrule pointer-tag sc2c
  ((#?ptr) "*"))

(defrule function-tag sc2c
  ((#?fn #?csym::fn) x))

(defrule vaarg-tag sc2c
  ((#?va-arg) "..."))

(defrule type-specifier sc2c
  ((#?,tspc[build-in-type]
    #?,tspc[struct-or-union-specifier]
    #?,tspc[enum-specifier]
    #?,tspc[typedef-name])
   (get-retval tspc))
  )

(defrule build-in-type sc2c
  ((#?void)                "void")
  ((#?char)                "char")
  ((#?signed-char)         "signed char")
  ((#?unsigned-char)       "unsigned char")
  ((#?short)               "short")
  ((#?signed-short)        "signed short")
  ((#?unsigned-short)      "unsigned short")
  ((#?int)                 "int")
  ((#?signed-int)          "signed int")
  ((#?unsigned-int)        "unsigned int")
  ((#?long)                "long")
  ((#?signed-long)         "signed long")
  ((#?unsigned-long)       "unsigned long")
  ((#?long-long)           "long long")
  ((#?signed-long-long)    "signed long long")
  ((#?unsigned-long-long)  "unsigned long long")
  ((#?float)               "float")
  ((#?double)              "double")
  ((#?long-double)         "long double")
  )


(defrule struct-or-union-specifier sc2c
  ((#?(,s-u[struct-or-union] ,id[identifier]))
   (list (get-retval s-u) " " (get-retval id)))
  )

(defrule struct-or-union sc2c
  ((#?struct)  -> "struct")
  ((#?union)   -> "union")
  )

(defrule enum-specifier sc2c
  ((#?(,en[enum-tag] ,id[identifier]))
   (list (get-retval en) " " (get-retval id)))
  )

(defrule enum-tag sc2c
  ((#?enum) "enum"))

(defrule type-qualifier sc2c
  ((#?const)    -> "const")
  ((#?restrict) -> "restrict")
  ((#?volatile) -> "volatile")
  )

(defrule typedef-name sc2c
  ((#?,id[identifier])
   (get-retval id))
  )

;;;; 4.Statements

(defrule statement sc2c
  ;; <== [trick] remove redundant block
  ((#?(begin (,stag[statement-tag] ,@rem)))
   (statement ~(,stag ,@rem)) )
  ((#?(loop (begin ,@rem)))
   (statement ~(loop ,@rem)))
  ;; ==>
  ((#?,stat[compound-statement]
    #?,stat[selection-statement]
    #?,stat[iteration-statement]
    #?,stat[jump-statement]
    #?,stat[labeled-statement]
    #?,stat[expression-statement]
    )
   (get-retval stat))

  ((#?())
   (list ";" #\Newline))
  )

(defrule compound-statement sc2c
  ((#?(begin ,@bi-list[block-item]))
   (list "{" #\Newline
         (get-retval bi-list)
         "}" #\Newline) )
  ((#?(let (,@decl-list[declaration]) ,@bi-list[block-item]))
   (list "{" #\Newline
         (get-retval decl-list)
         #\Newline
         (get-retval bi-list)
         "}" #\Newline))
  )

(defrule block-item sc2c
  ((#?,bi[inlined-declaration]
    #?,bi[statement])
   (get-retval bi))
  (otherwise
   (list "/*Error: " (write-to-string x) "*/"))
  )

(defrule labeled-statement sc2c
  ((#?(label ,id[identifier] ,stat[statement]))
   (list (get-retval id) ": " (get-retval stat)))
  ((#?(case ,exp[expression]))
   (list "case " (get-retval exp) ":" #\Newline))
  ((#?(default))
   (list "default:" #\Newline))
  )

(defrule expression-statement sc2c
  ((#?,exp-stat[expression])
   (list (get-retval exp-stat) ";" #\Newline))
  )

(defrule selection-statement sc2c
  ;; [trick] 余分なbeginをとる（↓のtrickが使えるパターンを増やすため）
  (#?(if ,exp (begin (,stag[statement-tag] ,@rem)))
   (statement ~(if ,exp (,stag ,@rem))))
  ;;
  (#?(cond ,@clauses)
   (statement (sc2c:cond->if clauses)))
  (#?(if ,exp[expression] ,stat1[statement] {,stat2[statement]})
   ;; [trick] sc1.ruleでできた if + do-while を while, forにできればする
   (when (and (not (pattern-variable-p stat2))
              (consp stat1)
              (eq ~do-while (car stat1)))
     (cond ((equal exp (second stat1))
            (return-from selection-statement
              (iteration-statement ~(while ,exp ,@(cddr stat1)))))
           ((and (consp (second stat1))
                 (eq ~exps (car (second stat1)))
                 (equal exp (car (last (second stat1)))))
            (return-from selection-statement
              (iteration-statement
               ~(for (,exp ,(butlast (second stat1))) ,@(cddr stat1)))))))
   (list* "if(" (get-retval exp) " )" #\Newline
          (get-retval stat1)
          (if-pattern-variable stat2
                               (list " else " (get-retval stat2))
                               "else;")) )
  (#?(switch ,exp[expression] ,@bi-list[block-item])
   (list "switch( " (get-retval exp) " )" #\Newline
         "{" #\Newline (get-retval bi-list) "}" #\Newline) )
  )

(defrule iteration-statement sc2c
  ;; [trick] 余分なbeginをとる
  ((#?(,tag[~(while do-while for)] ,exp (begin ,@body)))
   (iteration-statement ~(,tag ,exp ,@body)))
  ;;
  ((#?(while ,exp[expression] ,@bi-list[block-item])
    #?(do-while ,exp[expression] ,@bi-list[block-item]))
   (let ((prev nil) (post nil))
     (case (if (eql 1 exp) ~while (car x)) ; (do-while 1 ...) なら (while 1 ...) にする
       ((sc::while) (setq prev (list "while( " (get-retval exp) " )")
                          post ""))
       ((sc::do-while) (setq prev "do "
                             post (list "while( " (get-retval exp) " );")))
       (otherwise (error "unexpected keyword ~S in iteration-statement."
                         (car x))))
     (list prev                         ; "while" or "do"
           #\Newline 
           "{" #\Newline (get-retval bi-list)
           "}" post                     ; "while(...);" of do-while if necessary
           #\Newline)) )
  ((#?(loop ,@bi-list[block-item]))
   (iteration-statement ~(while 1 ,@bi-list)))

  ((#?(for (,@exp-list[expression] 
                      ,exp1[expression] ,exp2[expression])
           ,@bi-list[block-item]))
   (let ((exp-retval-list 
          (combine-each-nth 
           (multiple-value-list (get-retval exp-list)))))
     (list "for( "
           (separate-list (mapcar #'(lambda (xx)
                                      (sc2c:parenthesize xx 16))
                                  exp-retval-list)
                          ", ")
           " ; " (get-retval exp1)
           " ; " (get-retval exp2)
           " )" #\Newline 
           "{" #\Newline (get-retval bi-list)
           "}" #\Newline)) )
  ((#?(for (,@idecl-list[inlined-declaration] 
                        ,exp1[expression] ,exp2[expression])
           ,@bi-list[block-item]))
   (list "{" #\Newline (get-retval idecl-list)
         (iteration-statement ~(for (,exp1 ,exp2) ,@bi-list))
         "}" #\Newline) )
  )

(defrule jump-statement sc2c
  ((#?(goto ,id[identifier]))
   (list "goto " (get-retval id) ";" #\Newline))
  ((#?(continue))
   (list "continue;" #\Newline))
  ((#?(break))
   (list "break;" #\Newline))
  ((#?(return { ,exp[expression] }))
   (list "return " (if-pattern-variable exp
                       (get-retval exp))
         ";" #\Newline))
  )

;;; statement tag
(defrule statement-tag sc2c
  ((#?,tag[labeled-statement-tag]
    #?,tag[compound-statement-tag]
    #?,tag[selection-statement-tag]
    #?,tag[iteration-statement-tag]
    #?,tag[jump-statement-tag])
   (get-retval tag))
  )

(defrule labeled-statement-tag sc2c
  ((#?label)        ":")
  ((#?case)         "case")
  ((#?default)      "defalut")
  )

(defrule compound-statement-tag sc2c
  ((#?begin)        "")
  ((#?let)          "")                 ; sc1
  )

(defrule selection-statement-tag sc2c
  ((#?if)           "if")
  ((#?cond)         "cond")
  ((#?switch)       "switch")
  )

;;; 返り値は今のところ意味なし
(defrule iteration-statement-tag sc2c
  ((#?while)        "while")            ; sc1
  ((#?do-while)     (cons "do" "while"))
  ((#?for)          "for")              ;sc1
  ((#?loop)         (cons "do" "while(1)")) ;sc1
  )

(defrule jump-statement-tag sc2c
  ((#?goto)         "goto")
  ((#?continue)     "continue")
  ((#?break)        "break")
  ((#?return)       "return")
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; 5.Expressions

(defrule expression sc2c
  ;; number, character, string-literal
  ((#?,id[identifier])
   (and (not (member id *sc2c-typename-list* :test #'eq))
        (values (get-retval id) 0) ) )
  ((#?,num[sc-number])
   (values (get-retval num) 0) )
  ((#?,char[sc-character])
   (values (get-retval char) 0) )
  ((#?,str[sc-string])
   (values (get-retval str) 0) )
  ;; compound-literal
  ((#?(init ,texp[type-expression] ,cmp-init[compound-initializer]))
   (let ((assoc 3))
     (values (list "(" (funcall (get-retval texp) "") ") "
                   (get-retval cmp-init))
             assoc)) )
  ;; aref
  ((#?(aref ,exp1[expression] ,@exp-list[expression]))
   (if (null exp-list)
       (get-retval exp1)
     (let ((exp1-retval (multiple-value-list (get-retval exp1)))
           (exp-retval-list
            (combine-each-nth
             (multiple-value-list (get-retval exp-list))))
           (assoc 1))
       (values
        (list (sc2c:parenthesize exp1-retval assoc)
              "[" (separate-list 
                   (mapcar #'(lambda (xx) (sc2c:parenthesize xx 17))
                           exp-retval-list)
                   "][")
              "]")
        assoc))) )
  ;; fref
  ((#?(fref ,exp[expression] ,@fi-list[field-identifier]))
   (let ((exp-retval (multiple-value-list (get-retval exp)))
         (fi-str-list0 (get-retval fi-list))
         (fi-str-list nil)
         (assoc 1))
     (do ((str fi-str-list0 (cdr str)))
         ((endp str) (setq fi-str-list (reverse fi-str-list)))
       (if (eq '-> (car str))
           (progn
             (push "->" fi-str-list)
             (setq str (cdr str)))
         (push "." fi-str-list))
       (push (car str) fi-str-list))         
     (values (list (sc2c:parenthesize exp-retval assoc)
                   fi-str-list)
             assoc)) )
  ;; inc,dec
  ((#?(inc ,exp[expression])
    #?(dec ,exp[expression]))
   (let ((exp-retval (multiple-value-list (get-retval exp)))
         (op-str (postfix-operator (car x)))
         (assoc 1))
     (assert op-str)
     (values (list (sc2c:parenthesize exp-retval assoc) op-str)
             assoc)) )
  ;; unary-operator
  ((#?(sizeof ,texp[type-expression]))
   (let ((texp-str (funcall (get-retval texp) ""))
         (assoc 2))
     (values (list "sizeof(" texp-str ")")
             assoc)) )
  ((#?(,uop[unary-operator] ,exp[expression])) ; includes 'sizeof'
   (let ((exp-retval (multiple-value-list (get-retval exp)))
         (uop-retval (multiple-value-list (get-retval uop))))
     (let ((uop-str (first uop-retval))
           (uop-assoc (second uop-retval)))
       (values (list uop-str (sc2c:parenthesize exp-retval uop-assoc))
               uop-assoc))) )
  ((#?(+ ,exp[expression])
    #?(- ,exp[expression]))
   (let ((exp-retval (multiple-value-list (get-retval exp)))
         (assoc 2))
     (values (list (unary-operator (car x))
                   (sc2c:parenthesize exp-retval assoc))
             assoc)) )
  ((#?(/ ,exp[expression]))
   (expression ~(/ 1 ,exp)) )
  ;; cast
  ((#?(cast ,texp[type-expression] ,exp[expression]))
   (let ((texp-str (funcall (get-retval texp) ""))
         (exp-retval (multiple-value-list (get-retval exp)))
         (assoc 3))
     (values (list "(" texp-str ")" (sc2c:parenthesize exp-retval assoc))
             assoc)) )
  ;; operator
  ((#?(,op[operator]))
   (let ((op-noarg (third (multiple-value-list (get-retval op)))))
     (if op-noarg
         (expression op-noarg)
       (error "~A requires at least one argument" op))) )
  ((#?(,op[binary-operator] ,@exp-list[expression]))
   (let ((op-retval (multiple-value-list (get-retval op)))
         (exp-retval-list
          (combine-each-nth
           (multiple-value-list (get-retval exp-list)))))
     (let ((op-str (first op-retval))
           (op-assoc (second op-retval)))
       (values
        (separate-list
         (cons (sc2c:parenthesize (first exp-retval-list)
                                  (if op-assoc (1+ op-assoc) nil))
               (mapcar #'(lambda (xx)
                           (sc2c:parenthesize xx op-assoc))
                       (cdr exp-retval-list)))
         op-str)
        op-assoc))) )
  ;; comparator
  ((#?(,comp[comparator] ,exp1[expression] ,exp2[expression]))
   (let ((comp-retval (multiple-value-list (get-retval comp)))
         (exp1-retval (multiple-value-list (get-retval exp1)))
         (exp2-retval (multiple-value-list (get-retval exp2))))
     (let ((comp-str (first comp-retval))
           (comp-assoc (second comp-retval)))
       (values (list (sc2c:parenthesize exp1-retval comp-assoc)
                     " " comp-str
                     " " (sc2c:parenthesize exp2-retval comp-assoc))
               comp-assoc))))
  ;; if-exp
  ((#?(if-exp ,exp1[expression] ,exp2[expression] ,exp3[expression]))
   (let ((exp1-retval (multiple-value-list (get-retval exp1)))
         (exp2-retval (multiple-value-list (get-retval exp2)))
         (exp3-retval (multiple-value-list (get-retval exp3)))
         (assoc 14))
     (values (list (sc2c:parenthesize exp1-retval assoc)
                   "?" (sc2c:parenthesize exp2-retval assoc)
                   ":" (sc2c:parenthesize exp3-retval assoc))
             assoc)) )
  ;; assignment-operator
  ((#?(,as-op[assignment-operator] ,exp1[expression] ,exp2[expression]))
   (let ((as-retval (multiple-value-list (get-retval as-op)))
         (exp1-retval (multiple-value-list (get-retval exp1)))
         (exp2-retval (multiple-value-list (get-retval exp2))))
     (let ((as-str (first as-retval))
           (as-assoc (second as-retval)))
       (values (list (sc2c:parenthesize exp1-retval as-assoc)
                     " " as-str
                     " " (sc2c:parenthesize exp2-retval (1+ as-assoc)))
               as-assoc))) )
  ;; exps
  ((#?(exps ,@exp-list[expression]))
   (let ((exp-retval-list
          (combine-each-nth
           (multiple-value-list (get-retval exp-list))))
         (assoc 16))
     (values (separate-list (mapcar #'(lambda (xx)
                                        (sc2c:parenthesize xx assoc))
                                    exp-retval-list)
                            ",")
             assoc)) )
  ;; statement-expression (gcc extension)
  (#?(stat-exp ,@bi-list[block-item])
   (let ((assoc 1))
     (values (list "({" #\Newline (get-retval bi-list) "})")
             assoc)) )
  ;;  C-expression
  ((#?,cexp[c-expression])
   (get-retval cexp) )
  ;; funcion-call
  ((#?(,fexp[expression] ,@exp-list[expression]))
   (let ((fexp-retval (multiple-value-list (get-retval fexp)))
         (exp-retval-list
          (combine-each-nth
           (multiple-value-list (get-retval exp-list))))
         (assoc 1))
     (values (list (sc2c:parenthesize fexp-retval assoc)
                   "(" (separate-list 
                        (mapcar #'(lambda (xx) (sc2c:parenthesize xx 16))
                                exp-retval-list)
                        ", ")
                   ")")
             assoc)) )
  )

(defrule sc-number sc2c
  ((#?,num[#'floatp]
    #?,num[#'integerp])
   (write-to-string num)))

(defrule sc-character sc2c
  ((#?,ch[#'sc2c:char-sc2c])
   (list "'" (get-retval ch) "'" )) )

(defrule sc-string sc2c
  ((#?,str[#'sc2c:string-sc2c])
   (list #\" (get-retval str) #\") ))

;;; operator
;;; 返り値は (<Cでの表現> <優先度> {<単位元>})
(defrule operator sc2c
  ((#?,op[postfix-operator]
    #?,op[unary-operator]
    #?,op[binary-operator]
    #?,op[cast-operator]
    #?,op[comparator]
    #?,op[conditional-operator]
    #?,op[assignment-operator]
    #?,op[comma-operator]
    #?,op[cexp-operator])
   (get-retval op))
  )

(defrule binary-operator sc2c
  ((#?,op[arithmetic-operator]
    #?,op[bitwise-shift-operator]
    #?,op[bitwise-operator]
    #?,op[logical-operator])
   (get-retval op))
  )

(defrule postfix-operator sc2c
  ((#?fref)    (values "." 1))
  ((#?aref)    (values (cons "[" "]") 1))
  ((#?inc)     (values "++" 1))
  ((#?dec)     (values "--" 1))
  ((#?init)    (values :compound-literal 1)) ; for compound-literal
  )

(defrule unary-operator sc2c
  ((#?++)      (values "++" 2))
  ((#?--)      (values "--" 2))
  ((#?ptr)     (values "&" 2))
  ((#?mref)    (values "*" 2))
  ((#?+)       (values "+" 2 ~0))
  ((#?-)       (values "-" 2 ~0))
  ((#?bit-not) (values "~" 2))
  ((#?not)     (values "!" 2))
  ((#?sizeof)  (values "sizeof" 2))
  )

(defrule cast-operator sc2c
  ((#?cast)    (values ""  3))
  )

(defrule arithmetic-operator sc2c
  ((#?,op[multiplicative-operator]
    #?,op[additive-operator])
   (get-retval op))
  )

(defrule multiplicative-operator sc2c
  ((#?*)       (values "*" 4 ~1))
  ((#?/)       (values "/" 4))
  ((#?%)       (values "%" 4))
  )

(defrule additive-operator sc2c
  ((#?+)       (values "+" 5 ~0))
  ((#?-)       (values "-" 5 ~0))
  )

(defrule bitwise-shift-operator sc2c
  ((#?<<)      (values "<<" 6))
  ((#?>>)      (values ">>" 6))
  )

(defrule comparator sc2c
  ((#?<)       (values "<" 7))
  ((#?>)       (values ">" 7))
  ((#?<=)      (values "<=" 7))
  ((#?>=)      (values ">=" 7))
  ((#?==)      (values "==" 8))
  ((#?!=)      (values "!=" 8))
  )

(defrule bitwise-operator sc2c
  ((#?bit-and) (values "&" 9 ~1))
  ((#?bit-xor) (values "^" 10))
  ((#?bit-or)  (values "|" 11 ~0))
  )

(defrule logical-operator sc2c
  ((#?and)      (values "&&" 12 ~1))
  ((#?or)       (values "||" 13 ~0))
  )

(defrule conditional-operator sc2c
  ((#?if-exp)  (values (cons "?" ":") 14))
  )

(defrule assignment-operator sc2c
  ((#?=)        (values "=" 15))
  ((#?*=)       (values "*=" 15))
  ((#?/=)       (values "/=" 15))
  ((#?%=)       (values "%=" 15))
  ((#?+=)       (values "+=" 15))
  ((#?-=)       (values "-=" 15))
  ((#?<<=)      (values "<<=" 15))
  ((#?>>=)      (values ">>=" 15))
  ((#?bit-and=) (values "&=" 15))
  ((#?bit-xor=) (values "^=" 15))
  ((#?bit-or=)  (values "|=" 15))
  )

(defrule comma-operator sc2c
  ((#?exps)   (values "," 16))
  )

(defrule cexp-operator sc2c
  ((#?c-exp)   (values "" 16))
  )

;; field-identifier
(defrule field-identifier sc2c
  ((#?->)
   '->)
  ((#?,id[identifier])
   (get-retval id) )
  )


;; identifier
(defrule (c-keyword :memoize) sc2c
  ((#?,kw[~(auto break case char const
                 continue default do double
                 else enum extern float for
                 goto if inline int long  
                 register restrict return short
                 signed sizeof static struct
                 switch typedef union unsigned
                 void volatile while
                 -Bool -Complex -Imaginary)])
   x)
  )
(defrule identifier sc2c
  ((#?c2sc::$abstract) "")
  ((#?,id[#'(lambda (x)
              (and (symbolp x)
                   (not (c-keyword? x))))])
   (identifier0 id)) )

;; keywordでないチェックを省きたいときはこちら
(defrule identifier0 sc2c
  ((#?,id[#'sc2c:identifier-sc2c])
   (get-retval id)))

(defrule (sc-keyword :memoize) sc2c
  ((#?,sym[function-tag]
    #?,sym[function-attribute]
    #?,sym[vaarg-tag]
    #?,sym[deftype-tag]
    #?,sym[enum-tag]
    #?,sym[struct-or-union]
    #?,sym[array-tag]
    #?,sym[pointer-tag]
    #?,sym[storage-class-specifier]
    #?,sym[compound-storage-class-specifier]
    #?,sym[build-in-type]
    #?,sym[type-qualifier]
    #?,sym[statement-tag]
    #?,sym[designator-tag]
    #?,sym[operator]
    #?c-exp)
   (get-retval sym)))

;;;; 6.C-expressions
(defrule c-expression sc2c
  ((#?(,cop[cexp-operator] ,str[#'stringp] ,@exp-list[expression]))
   (let ((exp-retval-list
          (combine-each-nth (multiple-value-list (get-retval exp-list)))))
     (apply #'format nil str (mapcar #'(lambda (xx) 
                                         (string+-rec
                                          (sc2c:parenthesize xx 1)))
                                     exp-retval-list))) )
  )
